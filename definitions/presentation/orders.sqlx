js {
  const paymentMethods = ["credit_card", "coupon", "bank_transfer", "gift_card"];
}

config {
  type: "table",
  schema: "presentation"
}

with orders as (
  select * from ${ref("stg_orders")}
),

payments as (
  select * from ${ref("stg_payments")}
),

order_payments as (
  select
    order_id,
    ${paymentMethods
      .map(
        (paymentMethod) =>
          `sum(case when payment_method = '${paymentMethod}' then amount else 0 end) as ${paymentMethod}_amount`
      )
      .join(",\n    ")},
    sum(amount) as total_amount
  from payments
  group by order_id
),

final as (
  select
    orders.order_id,
    orders.customer_id,
    orders.order_date,
    orders.status,
    ${paymentMethods
      .map((paymentMethod) => `order_payments.${paymentMethod}_amount`)
      .join(",\n    ")},
    order_payments.total_amount as amount
  from orders
  left join order_payments
    on orders.order_id = order_payments.order_id
)

select * from final
